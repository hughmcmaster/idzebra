dnl Zebra
dnl (c) Index Data 1994-1999
dnl See the file LICENSE for details.
dnl $Id: configure.in,v 1.5 1999-05-20 12:57:18 adam Exp $
AC_INIT(include/zebraver.h)
dnl ------ Substitutions
AC_SUBST(ODEFS)
AC_SUBST(DEFS)
AC_SUBST(TCL_INCLUDE)
AC_SUBST(TCL_LIB)
dnl
dnl ------ Checking programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
TCL_LIB=""
TCL_INCLUDE=""
tclconfig=NONE
AC_ARG_WITH(tclconfig, [  --with-tclconfig        Path for tclConfig.sh], [tclconfig=$withval])
if test "x$tclconfig" = xNONE; then
	saveprefix=${prefix}
	AC_PREFIX_PROGRAM(tclsh)
	tclconfig=${prefix}/lib
	prefix=${saveprefix}
fi
if test -r ${tclconfig}/tclConfig.sh; then
	AC_MSG_CHECKING(for Tcl)
	. ${tclconfig}/tclConfig.sh
	TCL_LIB="$TCL_LIB_SPEC $TCL_LIBS"
	TCL_INCLUDE=-I${TCL_PREFIX}/include
        SHLIB_CFLAGS=$TCL_SHLIB_CFLAGS
        SHLIB_LD=$TCL_SHLIB_LD
        SHLIB_SUFFIX=$TCL_SHLIB_SUFFIX
        SHLIB_VERSION=$TCL_SHLIB_VERSION
        AC_MSG_RESULT($TCL_VERSION)
	ODEFS="-DHAVE_TCL_H=1"
fi
dnl
dnl ----- Sockets
checkBoth=0
AC_CHECK_FUNC(connect)
if test "$ac_cv_func_connect" = "no"; then
	AC_CHECK_LIB(socket, main, LIBS="$LIBS -lsocket", checkBoth=1)
fi
if test "$checkBoth" = "1"; then
	oldLibs=$LIBS
	LIBS="$LIBS -lsocket -lnsl"
	AC_CHECK_FUNC(accept, , [LIBS=$oldLibs])
fi
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, main, [LIBS="$LIBS -lnsl"]))
dnl
dnl ------ tcpd
AC_CHECK_LIB(wrap, main, [LIBS="$LIBS -lwrap"])
dnl
dnl ------ Headers
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl
dnl ------ Makefiles
AC_OUTPUT(Makefile util/Makefile bfile/Makefile dfa/Makefile dict/Makefile isam/Makefile isamc/Makefile recctrl/Makefile rset/Makefile index/Makefile)
