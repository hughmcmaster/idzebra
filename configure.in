dnl Zebra, Index Data Aps, 1995-2005
dnl $Id: configure.in,v 1.91.2.14 2005-08-29 12:59:49 adam Exp $
dnl
AC_INIT(include/zebraver.h)
AM_INIT_AUTOMAKE(idzebra,1.3.29)
dnl ------ Substitutions
AC_SUBST(TCL_INCLUDE)
AC_SUBST(TCL_LIB)
AC_SUBST(READLINE_LIBS)
dnl ------ Perl substitutions
AC_SUBST(PERL_BINARY)
AC_SUBST(PERL_XS_INIT)
AC_SUBST(PERL_XS_INIT_INCLUDE)
AC_SUBST(PERL_LIBS)
AC_SUBST(PERL_CFLAGS)
AC_SUBST(ZPERL_LIBS)
dnl
dnl ------ Checking programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB
dnl
dnl ------ threads
AC_ARG_ENABLE(threads, [  --disable-threads       disable threads],[enable_threa
ds=$enableval],[enable_threads=yes])
if test "$enable_threads" = "yes"; then
	yazflag=threads
else
	yazflag=""
fi
YAZ_INIT($yazflag,2.0.18)
YAZ_DOC
dnl ------ Look for Tcl
dnl See if user has specified location of tclConfig.sh; otherwise
dnl see if tclConfig.sh exists in same prefix lcoation as tclsh; otherwise
dnl disable Tcl.
TCL_LIB=""
TCL_INCLUDE=""
tclconfig=NONE
AC_ARG_WITH(tclconfig, [  --with-tclconfig=DIR    tclConfig.sh in DIR], [tclconfig=$withval])
if test "x$tclconfig" = xNONE; then
	saveprefix=${prefix}
	AC_PREFIX_PROGRAM(tclsh)
	tclconfig=${prefix}/lib
	prefix=${saveprefix}
	if test ! -r ${tclconfig}/tclConfig.sh; then
		# Not found, try search for Tcl on Debian systems.
		for d in /usr/lib/tcl*; do
			if test -f $d/tclConfig.sh; then
				tclconfig=$d
			fi
		done
	fi
fi
AC_MSG_CHECKING(for Tcl)
if test -r ${tclconfig}/tclConfig.sh; then
	. ${tclconfig}/tclConfig.sh
	if test -r ${tclconfig}/../generic/tcl.h; then
		TCL_INCLUDE=-I${tclconfig}/../generic
		TCL_LIB="$TCL_BUILD_LIB_SPEC $TCL_LIBS"
 	elif test -d ${TCL_PREFIX}/include/tcl${TCL_VERSION}; then
		TCL_INCLUDE=-I${TCL_PREFIX}/include/tcl${TCL_VERSION}
		TCL_LIB="$TCL_LIB_SPEC $TCL_LIBS"
	else
		TCL_INCLUDE=-I${TCL_PREFIX}/include
		TCL_LIB="$TCL_LIB_SPEC $TCL_LIBS"
	fi
	TCL_LIB=`echo $TCL_LIB|sed 's%-L/usr/lib%%g'`
        SHLIB_CFLAGS=$TCL_SHLIB_CFLAGS
        SHLIB_LD=$TCL_SHLIB_LD
        SHLIB_SUFFIX=$TCL_SHLIB_SUFFIX
        SHLIB_VERSION=$TCL_SHLIB_VERSION
        AC_MSG_RESULT($TCL_VERSION)
	AC_DEFINE(HAVE_TCL_H,1)
else
        AC_MSG_RESULT(Not found)
	AC_DEFINE(HAVE_TCL_H,0)
fi
dnl
dnl ------ times
AC_CHECK_HEADERS(sys/times.h)
dnl
dnl ------ crypt
AC_CHECK_LIB(crypt, crypt)
if test "$ac_cv_lib_crypt_crypt" = "yes"; then
	AC_CHECK_HEADERS(crypt.h)
fi
dnl
dnl ------ mkstemp
AC_CHECK_FUNCS(mkstemp)
dnl
dnl ------ GNU Readline
READLINE_SHARED_LIBADD=""
AC_CHECK_LIB(ncurses, tgetent, [READLINE_SHARED_LIBADD="-lncurses"],
        AC_CHECK_LIB(termcap, tgetent, [READLINE_SHARED_LIBADD="-ltermcap"])
)
READLINE_LIBS=""
AC_CHECK_LIB(readline, readline, [READLINE_LIBS="$READLINE_LIBS -lreadline $READLINE_SHARED_LIBADD"],,$READLINE_SHARED_LIBADD)
AC_CHECK_LIB(history, add_history, [READLINE_LIBS="$READLINE_LIBS -lhistory"])
if test "$ac_cv_lib_readline_readline" = "yes"; then
        AC_CHECK_HEADERS(readline/readline.h readline/history.h)
        xLIBS=$LIBS
        LIBS="$LIBS $READLINE_LIBS"
        AC_TRY_LINK([
        #include <stdio.h>
        #include <readline/readline.h>
        ],[
        static void f()
        {
                rl_attempted_completion_over = 0;
        }
        ],AC_DEFINE(HAVE_READLINE_COMPLETION_OVER))
        AC_TRY_LINK([
        #include <stdio.h>
        #include <readline/readline.h>
        ],[
        static void f()
        {
                rl_completion_matches (0, 0);
        }
        ],AC_DEFINE(HAVE_READLINE_RL_COMPLETION_MATCHES))
        LIBS=$xLIBS
fi
dnl
dnl ------ iconv
AC_ARG_WITH(iconv, [  --with-iconv[=DIR]        iconv library in DIR])
if test "$with_iconv" != "no"; then
	AC_MSG_CHECKING(for iconv)
	oldLIBS="$LIBS"
	oldCPPFLAGS="${CPPFLAGS}"
	if test "$with_iconv" != "yes" -a "$with_iconv" != ""; then
		LIBS="$LIBS -L${with_iconv}/lib"
		CPPFLAGS="${CPPFLAGS} -I${with_iconv}/include"
	fi
	AC_TRY_LINK([
		#include <iconv.h>
	],[
		static void f() {iconv_t t = iconv_open("", ""); }
	],[
		AC_DEFINE(HAVE_ICONV_H)
		AC_MSG_RESULT(yes)
	],[
		LIBS="$LIBS -liconv"
		AC_TRY_LINK([
			#include <iconv.h>
		],[
			static void f() {iconv_t t = iconv_open("", ""); }
		],[
			AC_DEFINE(HAVE_ICONV_H)
			AC_MSG_RESULT(yes)
		],[
			LIBS="$oldLIBS"
			CPPFLAGS="$oldCPPFLAGS"
			AC_MSG_RESULT(no)
		])
	])
fi
dnl
dnl ------- BZIP2
AC_CHECK_LIB(bz2,bzCompressInit)
if test "$ac_cv_lib_bz2_bzCompressInit" = "yes"; then
	AC_CHECK_HEADERS(bzlib.h)
else
	AC_CHECK_LIB(bz2,BZ2_bzCompressInit)
	if test "$ac_cv_lib_bz2_BZ2_bzCompressInit" = "yes"; then
		AC_CHECK_HEADERS(bzlib.h)
	fi
fi
dnl
dnl ------ -lm
AC_CHECK_LIB(m,sqrt)
dnl
dnl ------ EXPAT
expat=yes
AC_ARG_WITH(expat,   [  --with-expat[=DIR]        EXPAT library in DIR],[expat=$withval])
if test "$expat" != "no"; then
	xLIBS="$LIBS";
	xCFLAGS="$CFLAGS";
	if test "$expat" != "yes"; then
                EXPAT_CFLAGS="-I$expat/include"
                EXPAT_LIBS="-L$expat/lib"
		CFLAGS="$EXPAT_CFLAGS $CFLAGS"
		LIBS="$EXPAT_LIBS $LIBS"
	fi
	AC_CHECK_LIB(expat,XML_ParserCreate,[LIBS="$LIBS -lexpat"])
	if test "$ac_cv_lib_expat_XML_ParserCreate" = "yes"; then
		AC_CHECK_HEADERS(expat.h)
	else
		LIBS="$xLIBS"
		CFLAGS="$xCFLAGS"
	fi
fi
dnl
dnl ------ PERL
AM_CONDITIONAL(perl,false)
perl=no
PERL_XS_INIT="NULL"
PERL_XS_INIT_INCLUDE=''
PERL_BINARY=""
AC_ARG_WITH(perl,   [  --with-perl[=FILE]        perl binary location],[perl=$withval])
if test "$perl" != "no"; then
	AC_MSG_CHECKING(for perl binary)
	if test "$perl" = "yes"; then
	    perlbin=`which perl`
	else
            perlbin="$perl"
        fi 
        if test -x "$perlbin"; then
	    AC_MSG_RESULT($perlbin)	
	    AC_MSG_CHECKING(perl core directory)
	    archdir=`$perlbin -MConfig -e 'print $Config{archlib}'`;
	    perlcore="$archdir/CORE";
	    if test -d "$perlcore"; then
		PERL_BINARY="$perlbin"
		AC_MSG_RESULT($perlcore)	
	    else
		AC_MSG_RESULT(Failed)	
	    fi

	    AC_MSG_CHECKING("for ExtUtils::Embed to determine ccopts")
	    PERL_CFLAGS=`$perlbin -MExtUtils::Embed -e ccopts 2>/dev/null`
	    if test "$PERL_CFLAGS"; then
		AC_MSG_RESULT(OK)	
            else
		PERL_CFLAGS="-I$perlcore"
		AC_MSG_RESULT(Using defaults)	
	    fi

	    AC_MSG_CHECKING("for ExtUtils::Embed to determine ldflags")
	    PERL_LIBS=`$perlbin -MExtUtils::Embed -e ldopts 2>/dev/null`
	    if test "$PERL_LIBS"; then
		AC_MSG_RESULT(OK)	
            else
                PERL_LIBS="-L$perlcore -lperl -lm"
		AC_MSG_RESULT(Using defaults)	
	    fi

	    AC_MSG_CHECKING("for ExtUtils::Embed to create xs_init")
	    xsf="recctrl/xsinit.h"
	    `rm $xsf 2>/dev/null`;

	    `$perlbin -MExtUtils::Embed -e xsinit -- -o $xsf 2>/dev/null`
	    if test -r "$xsf"; then
		AC_MSG_RESULT(OK)	
		PERL_XS_INIT="xs_init"
		PERL_XS_INIT_INCLUDE='#include "xsinit.h"'
            else
		AC_MSG_RESULT(XS libraries are not going to be available)
            fi

	    xLIBS="$LIBS"
	    xCFLAGS="$CFLAGS"
	    CFLAGS="$PERL_CFLAGS $CFLAGS"
	    LIBS="$PERL_LIBS $LIBS"

	    AC_MSG_CHECKING(for perl library)
            AC_TRY_LINK([
            #include <stdio.h>
             ],[
            static void f()
            {
		;
            }
		],AM_CONDITIONAL(perl,true)
	          AC_DEFINE(HAVE_PERL,1)
		  AC_MSG_RESULT(found)
	          ZPERL_LIBS="$xLIBS"
			,
                  AC_MSG_RESULT(not found)
	          AC_DEFINE(HAVE_PERL,0)
		  LIBS="$xLIBS"
		  CFLAGS="$xCFLAGS")
        else
	    AC_DEFINE(HAVE_PERL,0)
	    AC_MSG_RESULT(Not found)
	fi
fi
dnl ------- 64 bit files
AC_MSG_CHECKING(for LFS)
AC_TRY_RUN([#define _FILE_OFFSET_BITS 64
#include <sys/types.h>
#include <unistd.h>
#include <string.h>
#include <stdio.h>
#include <fcntl.h>
#include <errno.h>
int main(int argc, char **argv)
{
	off_t o;
	char tmp_str[32];
	int fd;
	struct flock area;
	if (sizeof(off_t) != 8) 
		exit (1);
	o = 2000000000;
	sprintf (tmp_str, "%Ld", o+o+o);
	if (strcmp (tmp_str, "6000000000"))
		exit (1);
	fd = creat ("config.tmp", 0644);
	if (fd < 0)
		exit (1);
	area.l_type = F_WRLCK;
	area.l_whence = SEEK_SET;
	area.l_len = area.l_start = 0L;
	if (fcntl(fd, F_SETLKW, &area))
		exit (1);
	close (fd);
	unlink ("config.tmp");
	exit (0);
}
],bits=64,bits=32,bits=32)
if test "$bits" = "64"; then
	AC_DEFINE(_FILE_OFFSET_BITS,64)
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi
dnl
dnl ------ ANSI C Header files
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl ------ Create Makefiles
AC_OUTPUT([
  Makefile
  util/Makefile
  bfile/Makefile
  dfa/Makefile
  dict/Makefile
  isamb/Makefile
  isams/Makefile
  isamc/Makefile
  isam/Makefile
  rset/Makefile
  data1/Makefile
  recctrl/Makefile
  recctrl/perlread.h
  index/Makefile
  include/Makefile
  tab/Makefile
  doc/Makefile
  doc/zebra.xml
  doc/zebrahtml.dsl
  doc/zebraprint.dsl
  doc/zebraphp.dsl
  doc/tkl.xsl
  test/Makefile test/gils/Makefile test/usmarc/Makefile test/api/Makefile
  test/rusmarc/Makefile test/cddb/Makefile test/malxml/Makefile 
  test/config/Makefile test/sort2/Makefile
  perl/Makefile.PL test/xelm/Makefile
  test/dmoz/Makefile test/xpath/Makefile test/sort/Makefile test/zsh/Makefile
  test/marcxml/Makefile test/charmap/Makefile test/codec/Makefile
  examples/Makefile examples/gils/Makefile examples/zthes/Makefile
  idzebra.spec
])
if test -x "$perlbin"; then
	res=`cd perl ; $perlbin Makefile.PL ; cd .. ;`;
fi
if test  -z "$YAZLIB"; then
	echo "YAZ was not found. Use --with-yaz=DIR to specify location."
	test -f /etc/debian_version && echo "Debian package libyaz-dev is required."
fi

