dnl Zebra, Index Data Aps, 1994-2002
dnl $Id: configure.in,v 1.29.2.2 2002-07-23 12:33:21 adam Exp $
dnl
AC_INIT(include/zebraver.h)
AC_MSG_CHECKING(for package)
if test -r ${srcdir}/LICENSE.zmbol; then
	AC_MSG_RESULT([Z'mbol])
	PROGPREFIX=zmbol
	AC_DEFINE(ZMBOL,1)
	AM_INIT_AUTOMAKE(zmbol,1.1.3)
else
	AC_MSG_RESULT([Zebra])
	PROGPREFIX=zebra
	AC_DEFINE(ZMBOL,0)
	AM_INIT_AUTOMAKE(zebra,1.1.3)
	if test ! -r ${srcdir}/isam; then
		mkdir ${srcdir}/isam
	fi
	touch ${srcdir}/isam/Makefile.in
	if test ! -r ${srcdir}/isamc; then
		mkdir ${srcdir}/isamc
	fi
	touch ${srcdir}/isamc/Makefile.in
	if test ! -r ${srcdir}/isamb; then
		mkdir ${srcdir}/isamb
	fi
	touch ${srcdir}/isamb/Makefile.in
fi
AM_CONDITIONAL(ISZMBOL,test $PACKAGE = zmbol)
dnl ------ Substitutions
AC_SUBST(DEFS)
AC_SUBST(TCL_INCLUDE)
AC_SUBST(TCL_LIB)
AC_SUBST(PROGPREFIX)
dnl
dnl ------ Checking programs
AC_PROG_CC
AC_PROG_CPP
AC_PROG_INSTALL
AC_PROG_RANLIB

YAZ_INIT(threads)
dnl
dnl ------ Look for Tcl
dnl See if user has specified location of tclConfig.sh; otherwise
dnl see if tclConfig.sh exists in same prefix lcoation as tclsh; otherwise
dnl disable Tcl.
TCL_LIB=""
TCL_INCLUDE=""
tclconfig=NONE
AC_ARG_WITH(tclconfig, [  --with-tclconfig=DIR    tclConfig.sh in DIR], [tclconfig=$withval])
if test "x$tclconfig" = xNONE; then
	saveprefix=${prefix}
	AC_PREFIX_PROGRAM(tclsh)
	tclconfig=${prefix}/lib
	prefix=${saveprefix}
fi
AC_MSG_CHECKING(for Tcl)
if test -r ${tclconfig}/tclConfig.sh; then
	. ${tclconfig}/tclConfig.sh
	if test -r ${tclconfig}/../generic/tcl.h; then
		TCL_INCLUDE=-I${tclconfig}/../generic
		TCL_LIB="$TCL_BUILD_LIB_SPEC $TCL_LIBS"
 	else
		TCL_INCLUDE=-I${TCL_PREFIX}/include
		TCL_LIB="$TCL_LIB_SPEC $TCL_LIBS"
	fi
        SHLIB_CFLAGS=$TCL_SHLIB_CFLAGS
        SHLIB_LD=$TCL_SHLIB_LD
        SHLIB_SUFFIX=$TCL_SHLIB_SUFFIX
        SHLIB_VERSION=$TCL_SHLIB_VERSION
        AC_MSG_RESULT($TCL_VERSION)
	AC_DEFINE(HAVE_TCL_H,1)
else
        AC_MSG_RESULT(Not found)
	AC_DEFINE(HAVE_TCL_H,0)
fi
dnl
dnl ------ times
AC_CHECK_HEADERS(sys/times.h)
dnl
dnl ------- BZIP2
AC_CHECK_LIB(bz2,bzCompressInit)
if test "$ac_cv_lib_bz2_bzCompressInit" = "yes"; then
	AC_CHECK_HEADERS(bzlib.h)
else
	AC_CHECK_LIB(bz2,BZ2_bzCompressInit)
	if test "$ac_cv_lib_bz2_BZ2_bzCompressInit" = "yes"; then
		AC_CHECK_HEADERS(bzlib.h)
	fi
fi
dnl ------- 64 bit files
AC_MSG_CHECKING(for LFS)
AC_TRY_RUN([#define _FILE_OFFSET_BITS 64
#include <sys/types.h>
#include <unistd.h>
#include <string.h>
#include <stdio.h>
#include <fcntl.h>
#include <errno.h>
int main(int argc, char **argv)
{
	off_t o;
	char tmp_str[32];
	int fd;
	struct flock area;
	if (sizeof(off_t) != 8) 
		exit (1);
	o = 2000000000;
	sprintf (tmp_str, "%Ld", o+o+o);
	if (strcmp (tmp_str, "6000000000"))
		exit (1);
	fd = creat ("config.tmp", 0644);
	if (fd < 0)
		exit (1);
	area.l_type = F_WRLCK;
	area.l_whence = SEEK_SET;
	area.l_len = area.l_start = 0L;
	if (fcntl(fd, F_SETLKW, &area))
		exit (1);
	close (fd);
	unlink ("config.tmp");
	exit (0);
}
],bits=64,bits=32,bits=32)
if test "$bits" = "64"; then
	AC_DEFINE(_FILE_OFFSET_BITS,64)
	AC_MSG_RESULT(yes)
else
	AC_MSG_RESULT(no)
fi
dnl
dnl ------ ANSI C Header files
AC_STDC_HEADERS
if test "$ac_cv_header_stdc" = "no"; then
	AC_MSG_WARN(Your system doesn't seem to support ANSI C)
fi
dnl ------ Create Makefiles
AC_OUTPUT([
  Makefile
  util/Makefile
  bfile/Makefile
  dfa/Makefile
  dict/Makefile
  isamb/Makefile
  isams/Makefile
  isamc/Makefile
  isam/Makefile
  rset/Makefile
  recctrl/Makefile
  index/Makefile
  include/Makefile
  tab/Makefile
  doc/Makefile
  test/Makefile test/gils/Makefile test/usmarc/Makefile
])
